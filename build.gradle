plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'signing'
    id 'ru.vyarus.java-lib' version '2.2.2'
    id 'ru.vyarus.github-info' version '1.3.0'
    id 'ru.vyarus.quality' version '4.6.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '7.2'
}

repositories { mavenLocal(); mavenCentral() }
dependencies {
    implementation 'com.google.inject:guice:5.0.1'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hibernate:hibernate-validator:7.0.1.Final'
    testImplementation 'org.glassfish:jakarta.el:4.0.2'
    testImplementation 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0'

    // was removed in jdk 15
    if(JavaVersion.current() >= JavaVersion.VERSION_15) {
        implementation 'org.openjdk.nashorn:nashorn-core:15.3'
    }
}

group = 'ru.vyarus'
description = 'Guice method validation through jakarta.validation implementation (hibernate-validator)'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

javaLib {
    // java 9 auto module name
    autoModuleName = "ru.vyarus.guice.validator"
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

afterReleaseBuild {
    dependsOn = ['publishToSonatype',
                 'closeAndReleaseSonatypeStagingRepository']
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    //useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
